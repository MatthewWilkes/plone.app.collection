*** Keywords ***

Suite Setup
    Open browser  ${front-page}

Suite Teardown
    Close All Browsers

Log in
    [Documentation]  Log in to the site as ${userid} using ${password}. There
    ...              is no guarantee of where in the site you are once this is
    ...              done. (You are responsible for knowing where you are and
    ...              where you want to be)
    [Arguments]  ${userid}  ${password}

    Go to  ${PLONE_URL}/login_form
    Page should contain element  __ac_name
    Page should contain element  __ac_password
    Page should contain button  Log in
    Input text  __ac_name  ${userid}
    Input text  __ac_password  ${password}
    Click Button  Log in

Log in as site owner
    [Documentation]  Log in as the SITE_OWNER provided by plone.app.testing,
    ...              with all the rights and privileges of that user.
    Log in  ${SITE_OWNER_NAME}  ${SITE_OWNER_PASSWORD}

Log in as test user

    Log in  ${TEST_USER_NAME}  ${TEST_USER_PASSWORD}

Log out
    Go to  ${PLONE_URL}/logout
    Page should contain  logged out

Add Page
    [arguments]  ${title}

    Open Add New Menu
    Click Link  link=Page
    Wait Until Page Contains Element  title
    Input Text  title  ${title}
    Click button  name=form.button.save
    Page Should Contain  Changes saved.

Add folder
    [arguments]  ${title}

    Open Add New Menu
    Click Link  css=#plone-contentmenu-factories a#folder
    Wait Until Page Contains Element  css=#archetypes-fieldname-title input
    Input Text  title  ${title}
    Click Button  Save
    Page should contain  ${title}
    Element should contain  css=#parent-fieldname-title  ${title}

Open Menu
    [Arguments]  ${elementId}

    Element Should Be Visible  css=dl#${elementId} span
    Element Should Not Be Visible  css=dl#${elementId} dd.actionMenuContent
    Click link  css=dl#${elementId} dt.actionMenuHeader a
    Wait until keyword succeeds  1  5  Element Should Be Visible  css=dl#${elementId} dd.actionMenuContent

Open Workflow Menu
    Open Menu  plone-contentmenu-workflow

Open Add New Menu
    Open Menu  plone-contentmenu-factories

Click ${name} In Edit Bar
    Page Should Contain Element  id=edit-bar  Edit bar is not available : edit permission missing ?
    Element Should Contain  css=div#edit-bar ul#content-views  ${name}
    Click Link  ${name}

Trigger Workflow Transition
    [Arguments]  ${transitionId}

    Open Workflow Menu
    Click Link  workflow-transition-${transitionId}
    Check Status Message  Item state changed.

Workflow Publish
    Trigger Workflow Transition  publish

Check Status Message
    [Arguments]  ${message}

    Page Should Contain Element  css=dl.portalMessage dt  Info
    Page Should Contain Element  css=dl.portalMessage dd  ${message}

Create Collection
    [Arguments]  ${title}
    Go to  ${test-folder}/++add++Collection
    Input text  name=form.widgets.IBasic.title  ${title}
    Click Button  Save
    Page should contain  ${title}

Live Preview number of results should be
    [Arguments]  ${number}
    Wait Until Page Contains  ${number} items matching your search terms.

Live Preview should contain
    [Arguments]  ${title}
    Page should contain  ${title}

Set Review state Criterion
    Wait Until Page Contains Element  xpath=//select[@name="addindex"]
    Select From List  xpath=//select[@name="addindex"]  Review state
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Click Element  xpath=//span[@class='arrowDownAlternative']
    Select Checkbox  published
    Click Button  Save

Set Absolute Location Criterion To
    [Arguments]  ${absolute_path_criterion}
    Wait Until Page Contains Element  xpath=//select[@name="addindex"]
    Select From List  xpath=(//select[@name="addindex"])[last()]  Location
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Select From List  xpath=(//select[@class='queryoperator'])[last()]  Absolute path
    #Set Selenium Timeout  600 seconds
    #Wait For Condition  true
    Input Text  name=form.widgets.ICollectionBehavior.query.v:records  ${absolute_path_criterion}
    Click Button  Save

Set Creator Criterion To
    [Arguments]  ${creator_criterion}
    Wait Until Page Contains Element  xpath=//select[@name="addindex"]
    Select From List  xpath=//select[@name="addindex"]  Creator
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Select From List  xpath=//select[@class='queryoperator']  Is
    Input Text  name=form.widgets.ICollectionBehavior.query.v:records  ${creator_criterion}
    Wait Until Page Contains  2 items matching your search terms.
    Live Preview should contain  Test user document
    Click Button  Save

Set Relative Location Criterion To
    [Arguments]  ${relative_path_criterion}
    Wait Until Page Contains Element  xpath=//select[@name="addindex"]
    Select From List  xpath=//select[@name="addindex"]  Location
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Select From List  xpath=//select[@class='queryoperator']  Relative path
    Input Text  xpath=//input[@name='form.widgets.ICollectionBehavior.query.v:records']  ${relative_path_criterion}
    Click Button  Save

Set Relative Location Without Subfolders Criterion To
    [Arguments]  ${relative_path_criterion}
    Wait Until Page Contains Element  xpath=//select[@name="addindex"]
    Select From List  xpath=(//select[@name="addindex"])[last()]  Location
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Select From List  xpath=(//select[@class='queryoperator'])[last()]  Relative path (without subfolders)
    Wait Until Page Contains Element  xpath=//input[@name='form.widgets.ICollectionBehavior.query.v:records']
    Input Text  name=form.widgets.ICollectionBehavior.query.v:records  ${relative_path_criterion}
    Click Button  Save

Set Short name (id) Criterion
    Wait Until Page Contains Element  xpath=//select[@name="addindex"]
    Select From List  xpath=//select[@name="addindex"]  Short name (id)
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Input Text  name=form.widgets.ICollectionBehavior.query.v:records  first-document
    Wait Until Page Contains  1 items matching your search terms.
    Page should contain  First Document
    Click Button  Save

Set Type Criterion
    Wait Until Page Contains Element  xpath=//select[@name="addindex"]
    Select From List  xpath=//select[@name="addindex"]  Type
    Wait Until Page Contains Element  xpath=//select[@class='queryoperator']
    Click Element  xpath=//span[@class='arrowDownAlternative']
    Select Checkbox  Document
    Wait Until Page Contains  1 items matching your search terms.
    Page should contain  My Document
    Click Button  Save

Add collection
    Go to  ${test-folder}/my-collection/edit
    Click Link  xpath=//div[@name='form.widgets.ICollectionBehavior.query.v:records:list']
    Click Link  collection
    Page should contain  Add Collection
    Page should contain  Description
    Page should contain  Search terms

    Input text  form.widgets.IBasic.title  My Collection
    Wait Until Page Contains Element  xpath=//select[@name="addindex"]
    Select From List  xpath=//select[@name="addindex"]  Description
    Input text  xpath=//div[@name='form.widgets.ICollectionBehavior.query.v:records  Plone

    Click Button  Save
    Page should contain  My Collection
